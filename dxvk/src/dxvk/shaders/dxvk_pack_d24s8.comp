#version 450

layout(
  local_size_x = 8,
  local_size_y = 8,
  local_size_z = 1) in;

layout(binding = 0, std430)
writeonly buffer s_buffer_t {
  uint data[];
} s_buffer;

layout(binding = 1) uniform  sampler2DArray u_depth;
layout(binding = 2) uniform usampler2DArray u_stencil;

layout(push_constant)
uniform u_info_t {
  uvec2 src_offset;
  uvec2 src_extent;
} u_info;

void main() {
  if (all(lessThan(gl_GlobalInvocationID.xy, u_info.src_extent))) {
    uvec3 src_offset = uvec3(
      gl_GlobalInvocationID.xy + u_info.src_offset,
      gl_GlobalInvocationID.z);

    uvec2 dst_pitch = uvec2(u_info.src_extent.x,
      u_info.src_extent.x * u_info.src_extent.y);

    uint dst_offset = gl_GlobalInvocationID.x
                    + gl_GlobalInvocationID.y * dst_pitch.x
                    + gl_GlobalInvocationID.z * dst_pitch.y;
    
    float depth   = texelFetch(u_depth,   ivec3(src_offset), 0).r;
    uint  stencil = texelFetch(u_stencil, ivec3(src_offset), 0).r;
    
    s_buffer.data[dst_offset] = uint(mix(0.0f, float((1 << 24) - 1), depth)) | (stencil << 24);
  }
}